# Extract subset of PM20 for CdV2021
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX frapo: <http://purl.org/cerif/frapo/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
construct {
  ?pm20 ?p ?o .
  ?pm20 zbwext:wdIdentifier ?wdId ;
    foaf:page ?page .
  ?pm20 schema:member ?member .
  ?member ?memberProp ?memberValue .
  ?pm20 zbwext:activity ?activity .
  ?activity ?actProp ?actValue .
  ?pm20 ?companyRelation ?otherCompany .
  ?otherCompany ?comProp ?comValue .
  ?pm20 gn:locatedIn ?geoLoc .
  ?geoLoc ?geoProp ?geoValue .
  ?pm20 gndo:relatedPerson ?relPers .
  ?relPers ?relPersProp ?relPersValue .
  ?pm20 dct:hasPart ?famPers .
  ?famPers ?famPersProp ?famPersValue .
  ?pm20 dct:isPartOf ?fam .
  ?fam ?famProp ?famValue .
}
where {
  values ( ?companyRelation ) {
    ( schema:parentOrganization )
    ( schema:subOrganization )
    ( gndo:preceedingCorporateBody )
    ( gndo:succeedingCorporateBody )
    ( gndo:relatedCorporateBody )
    ( zbwext:includesInstitutionNamed )
  }
  # only person and company folders
  ?pm20 a ?class .
  filter(?class in (zbwext:PersonFolder, zbwext:CompanyFolder))
  ?pm20 dct:identifier ?id .
  #
  # only folders with freely viewable documents
  ?pm20 zbwext:freeDocCount ?docCount .
  filter(?docCount > 0)
  #
  # restrict to example folders
  ##filter(?id in ("pe/005823", "co/041389")) .
  #
  # all available properties and values
  ?pm20 ?p ?o .
  # filter out redundant labels
  filter(!(?p = rdfs:label))
  #
  # roles by organization members
  optional {
    ?pm20 schema:member ?member .
    ?member ?memberProp ?memberValue .
  }
  #
  # activities of a person
  optional {
    ?pm20 zbwext:activity ?activity .
    ?activity ?actProp ?actValue .
  }
  #
  # relations of a company
  optional {
    ?pm20 ?companyRelation ?otherCompany .
    ?otherCompany ?comProp ?comValue .
  }
  #
  # related persons
  optional {
    ?pm20 gndo:relatedPerson ?relPers .
    ?relPers ?relPersProp ?relPersValue .
  }
  optional {
    ?pm20 dct:hasPart ?famPers .
    ?famPers ?famPersProp ?famPersValue .
  }
  optional {
    ?pm20 dct:isPartOf ?fam .
    ?fam ?famProp ?famValue .
  }
  #
  # geo locaton of a company
  optional {
    ?pm20 gn:locatedIn ?geoLoc .
    ?geoLoc ?geoProp ?geoValue .
  }
  #
  # information from Wikidata
  ?pm20 dct:identifier ?pm20Id .
  optional {
    graph <http://zbw.eu/beta/wikidata/ng> {
      ?wd wdt:P4293 ?pm20Id .
      bind(strafter(str(?wd), str(wd:)) as ?wdId)
      #
      # German Wikipedia page
      optional {
        ?page schema:about ?wd ;
              schema:isPartOf <https://de.wikipedia.org/> .
      }
    }
  }
}
