# Count values of a certain property within a substructure for PM20 folders
#
PREFIX schema: <http://schema.org/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?count (str(count(distinct ?pm20)) as ?countLabel) ?value
where {
  values ( ?sub ?property ) {
    ( zbwext:activity schema:about )
  }
  ?pm20 a zbwext:Pm20Folder ;
        ?sub [ ?property ?value ].
  ?pm20 zbwext:freeDocCount ?docCount .
  filter(?docCount > 0)
  bind(uri(concat('http://zbw.eu/beta/sparql-lab/?endpoint=http://zbw.eu/beta/sparql/pm20/query',
          '&queryRef=https://api.github.com/repos/zbw/cdv2021-pressemappe20/contents/sparql/folder_by_value_sub.rq',
          '&sub=', str(?sub), '&property=', str(?property), '&value="', ?value, '"')) as ?count)
}
group by ?value ?count
order by desc(count(?value))
